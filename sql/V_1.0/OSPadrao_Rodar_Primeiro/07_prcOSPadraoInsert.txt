SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[prcOSPadraoInsert]
	@p_DT_DATA_OS				DATETIME	= NULL,	
	@p_ST_STATUS_OS				INT		= NULL,
	@p_CD_TIPO_OS				INT		= NULL,
	@p_CD_CLIENTE				INT		= NULL,
	@p_CD_ATIVO_FIXO			VARCHAR(06)	= NULL,
	@p_CD_TECNICO				VARCHAR(06)	= NULL,
	@p_HR_INICIO				VARCHAR(5)	= NULL,
	@p_HR_FIM					VARCHAR(5)	= NULL,
	@p_nidUsuarioAtualizacao	NUMERIC(18,0)	= NULL,
	@p_ID_OS					BIGINT		OUTPUT

AS
BEGIN

	DECLARE @cdsErrorMessage		NVARCHAR(4000),
			@nidErrorSeverity		INT,
			@nidErrorState			INT,
			@nidLog					NUMERIC(18,0),
			@ID_LOG_STATUS_OS	BIGINT

	-- SET NOCOUNT ON added to prevent extra result sets from
	-- interfering with SELECT statements.
	SET NOCOUNT ON;

	BEGIN TRY

		BEGIN TRANSACTION

		INSERT INTO dbo.tbOSPadrao
		        ( DT_DATA_OS,
		          ST_STATUS_OS,
		          CD_TIPO_OS,
		          CD_CLIENTE,
		          CD_TECNICO,
				  CD_ATIVO_FIXO,
				  HR_INICIO,
				  HR_FIM,
				  nidUsuarioAtualizacao,
		          dtmDataHoraAtualizacao )
		VALUES
		        ( @p_DT_DATA_OS, 
		          @p_ST_STATUS_OS,
		          @p_CD_TIPO_OS,
		          @p_CD_CLIENTE,
		          @p_CD_TECNICO,
				  @p_CD_ATIVO_FIXO,
				  @p_HR_INICIO,
				  @p_HR_FIM,
				  @p_nidUsuarioAtualizacao,
		          GETDATE()
		          )

		SET @p_ID_OS = @@IDENTITY

		EXECUTE dbo.prcLogGravar 
					@p_nidLog			= @nidLog,
					@p_nidUsuarioAtualizacao	= @p_nidUsuarioAtualizacao,
					@p_ccdAcao			= 'I',
					@p_cnmTabela			= 'tbOSPadrao',
					@p_nidPK			= @p_ID_OS,
					@p_nidLogReturn			= @nidLog OUTPUT
	
		EXECUTE dbo.prcLogStatusOSPadraoInsert 
		    @p_ID_OS				= @p_ID_OS,
		    @p_DT_DATA_LOG_OS		= @p_DT_DATA_OS,
		    @p_ST_STATUS_OS			= @p_ST_STATUS_OS,
		    @p_nidUsuarioAtualizacao	= @p_nidUsuarioAtualizacao,
		    @p_ID_LOG_STATUS_OS		= @ID_LOG_STATUS_OS OUTPUT


		COMMIT TRANSACTION
	
	END TRY

	BEGIN CATCH

		SELECT	@cdsErrorMessage	= ERROR_MESSAGE(),
				@nidErrorSeverity	= ERROR_SEVERITY(),
				@nidErrorState		= ERROR_STATE();

		ROLLBACK TRANSACTION

		-- Use RAISERROR inside the CATCH block to return error
		-- information about the original error that caused
		-- execution to jump to the CATCH block.
		RAISERROR (@cdsErrorMessage, -- Message text.
				   @nidErrorSeverity, -- Severity.
				   @nidErrorState -- State.
				   )

	END CATCH

END

GO